apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'jacoco'

repositories {
  mavenCentral()
}

dependencies {
  testCompile group: 'junit', name: 'junit', version: '4.+'
  compile fileTree(dir: 'libs', include: '**/*.jar')
  runtime fileTree(dir: 'libs', include: '**/*.jar')
}

compileJava {
  options.encoding = 'UTF-8'
}

findbugsMain {
  excludeFilter = file('src/test/resources/findbugs/exclude-filter.xml')
}

// run test task before jacocoTestReport task.
jacocoTestReport.dependsOn test

sourceSets {
  def envValue = getEnvValue()

  main {
    resources {
      srcDir 'src/main/resources'
      srcDir 'envdepends/' + envValue + '/resources'
    }
  }
}

// Archive project task.
task dist(type: Zip, dependsOn: jar) {
  def envValue = getEnvValue()
  def distDir = new File('build/dist/' + envValue)
  destinationDir = distDir

  archiveName = "${baseName}_${envValue}.${extension}"

  from 'src/dist'
  into('lib') {
    from 'libs'
  }
  into('lib') {
    from jar.archivePath
  }
  into('conf') {
    from 'conf'
  }
  into('conf') {
    from 'envdepends/' + envValue + '/conf'
  }
}

def getEnvValue() {
  def envValue
  if(project.hasProperty('env')) {
    // -Penv=xx option
    envValue = env
  } else {
    // no -Penv=xx option
    envValue = 'dev'
  }
  println 'envValue = ' + envValue
  envValue
}
